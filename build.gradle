plugins {
    id 'nu.studer.credentials' version '2.1'
}

def artifactoryCredentials = {
    username = project.hasProperty('artifactory.username') ? project.property('artifactory.username') : System.properties['user.name']
    password = project.hasProperty('artifactory.password') ? project.property('artifactory.password') : credentials['artifactory.password']
}

repositories {
    maven {
        credentials artifactoryCredentials
        url "https://artifactory.sybit.de/libs-release"
        metadataSources {
            artifact()
        }
    }
}

configurations {
    runtime
}

dependencies {
    runtime "hybris:hybris-commerce-suite:${hybrisVersion}@zip"
	runtime "spartacus:spartacussampledata:2005@zip"
}

ext {
    isWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()
    hybrisDir = "${projectDir}/hybris"
    hybrisBinDir = "${hybrisDir}/bin"
    hybrisPlatformDir = "${hybrisBinDir}/platform"
    hybrisConfigDir = "${hybrisDir}/config"
	installerDir = "${projectDir}/installer"
    customConfigDir = "${projectDir}/customconfig"
	customRecipeDir = "${projectDir}/customrecipe"
    antWorkingDir = getAntCmdWorkingDir()
}

defaultTasks "install"

task copyCustomConfigFiles {
	dependsOn 'runInstaller'
	doLast {
		logger.lifecycle "copy and merge config of ${customConfigDir} with ${hybrisConfigDir}"
        copy {
            from customConfigDir
            into hybrisConfigDir
            exclude "custom.properties"
        }
		mergeLocalPropertiesFiles()
    }
}

task runInstaller(type: Exec) {
	dependsOn 'unzipHybris'
	workingDir = installerDir
	executable = isWindows ? "cmd" : "sh"
	args = isWindows ? ["/c"] << "install.bat -r cx-spartacus -A initAdminPassword=nimda" : ["-c"] << "install.sh -r cx-spartacus -A initAdminPassword=nimda"
	finalizedBy copyCustomConfigFiles
}

task copyCustomRecipe {
	doLast {
		copy {
			from customRecipeDir
			into "${installerDir}/recipes/cx-spartacus"
        }
	}
	finalizedBy runInstaller
}

task copyCustomLocalProperties {
	doLast {
		copy {
			from customConfigDir
			into "${installerDir}/customconfig"
			include "custom.properties"
        }
	}
	finalizedBy copyCustomRecipe
}

task unzipSpartacusSampleData {
    File zipFile = project.configurations.runtime.find { it.name.startsWith("spartacussampledata") }
    inputs.files zipFile
    doLast {
        logger.lifecycle "Unzipping ${zipFile.name}"
        copy {
            from zipTree(zipFile)
            into "${hybrisBinDir}/modules/spartacus"
        }
    }
	finalizedBy copyCustomLocalProperties
}

task unzipHybris {
    File zipFile = project.configurations.runtime.find { it.name.startsWith("hybris-commerce-suite") }
    inputs.files zipFile
    outputs.upToDateWhen { file(hybrisPlatformDir).exists() }
    doLast {
        if (file(hybrisBinDir).exists()) {
            //deleteHybrisBinDir()
        }
        delete hybrisConfigDir
        logger.lifecycle "Unzipping ${zipFile.name}"
        copy {
            from zipTree(zipFile)
            into projectDir
        }
    }
	finalizedBy unzipSpartacusSampleData
}

task install {
    group "Hybris Project Buildr"
    description "Installs SAP Commerce"
    dependsOn 'unzipHybris'
}

private void deleteHybrisBinDir() {
    logger.lifecycle "Deleting ${hybrisBinDir}"
    ant.delete(includeEmptyDirs: true) {
        fileset(dir: hybrisBinDir) {
            include(name: "**/*")
            exclude(name: "custom/**")
        }
    }
}

private void mergeLocalPropertiesFiles() {
    def properties = mergeProperties()
    properties.put('commerce.version', findProperty("commerce.version") ?: project.property('hybrisVersion'))
    properties.store new FileOutputStream(new File("${hybrisConfigDir}/local.properties")), 'Generated by Hybris Project Buildr and Hybris Installer'
}

private Properties mergeProperties() {
	List<File> propertyFiles = new ArrayList<>()
	propertyFiles.add(new File(hybrisConfigDir, "local.properties"))
	propertyFiles.add(new File(customConfigDir, "custom.properties"))

    def properties = new Properties() {
        @Override
        synchronized Enumeration<Object> keys() {
            return Collections.enumeration(new TreeSet<>(super.keySet()))
        }
    }
    propertyFiles.each { file ->
        if (file.exists()) {
            properties.load(new FileInputStream(file))
        }
    }
    properties
}

"${project.property('antTasks')}".tokenize(',').each { taskName ->
    task "ant_${taskName}"(type: Exec) {
        group "Hybris Build Framework"
        workingDir = antWorkingDir
        executable = isWindows ? "cmd" : "sh"
        args = isWindows ? ["/c"] << getWindowsAntCmd(taskName) : ["-c"] << getUnixAntCmd(taskName)
    }
}

private String getAntOpts(String antTask) {
    String antOpts = project.property("antOpts")
    if (antTask == "unittests") {
        project.properties.findAll {
            if (it.key.startsWith("testclasses") && !it.value.toString().isEmpty()) {
                antOpts <<= " -D${it.key}=${it.value}"
            }
        }
    }
    antOpts
}

private String getInstallOpts() {
    def command = "-r ${project.property('installer.recipe')}"
    workingDir
}

private String getAntCmdWorkingDir() {
    def workingDir = hybrisPlatformDir
    if (project.hasProperty("extension")) {
        def dirs = []
        file(customExtensionsDir).traverse(nameFilter: ~/extensioninfo.xml/, maxDepth: 1) {
            if (it.parentFile.name == project.property("extension")) {
                dirs.add(it.parentFile)
            }
        }
        if (dirs.isEmpty()) {
            throw new IllegalArgumentException("Extension ${project.property("extension")} not found")
        }
        workingDir = dirs[0].path
    }
    workingDir
}

private String getWindowsAntCmd(String antTask) {
    def antPreCommand = "set \"ANT_OPTS=${getAntOpts(antTask)}\" & " +
            "set \"PLATFORM_HOME=${hybrisPlatformDir}\" & " +
            "set \"ANT_HOME=${hybrisPlatformDir}\\apache-ant\" & " +
            "set \"PATH=${hybrisPlatformDir}\\apache-ant\\bin;%PATH%\""
    "${antPreCommand} & ant ${antTask}"
}

private String getUnixAntCmd(String antTask) {
    def antPreCommand = "export PLATFORM_HOME=\"${hybrisPlatformDir}\" && " +
            "export ANT_OPTS=\"${getAntOpts(antTask)}\" && " +
            "export ANT_HOME=\"${hybrisPlatformDir}/apache-ant\" && " +
            "chmod +x \"${hybrisPlatformDir}/apache-ant/bin/ant\" && "
    antPreCommand += ' export PATH=\"$ANT_HOME/bin:$PATH\" && '
    "${antPreCommand} ant ${antTask}"
}